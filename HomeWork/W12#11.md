## 課程:計算機結構 -- W12習題

## CPU
>* 圖的產生得大力參考CH2-4 ,程式搭配圖看 
![image](https://github.com/AIONLin/co109a/blob/master/Pics/WeeK12_CPU/CPU.jpg)
>*運作方法可更明白 CH2~CH4 
![image](https://github.com/AIONLin/co109a/blob/master/Pics/WeeK12_CPU/CPU2.jpg)
* CPU.hdl
<pre><code>CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    // 用or16 來命名 isC、c1、d2之類的後面比較好使用
    Or16(a=false ,b=instruction ,out[15]=isC ,out[12]=a ,out[11]=c1 ,out[10]=c2 ,out[9]=c3 ,out[8]=c4 ,out[7]=c5 ,out[6]=c6 ,out[5]=d1 , out[4]=d2 , out[3]=d3 , out[2]=j1 , out[1]=j2 , out[0]=j3);

    //從圖片左到右開始編碼 
    // 第一個 Mux16   c甲 = isC = 1 
    Mux16(a=instruction ,b=ALUoutput ,sel=isC ,out=intoA);    //  sel就是c甲

    // A register      c乙 = isA(0) 或 isC&d1(1)   = Aload
    Not(in=isC ,out=isA);        //  isC=I15  isA=!isC       進去的是A或C指令
    And(a=isC ,b=d1 ,out=isCd1);              //  whoareyou = isC & d1     
    Or(a=isA, b=isCd1, out=Aload);

    ARegister(in=intoA ,load=Aload ,out=ARout ,out[0..14]=addressM);    //  load = isA 或 (isC & d1)    addressM = ARout[0..14]

    // 第二個 Mux16   c丙  = a 
    Mux16(a=ARout ,b=inM ,sel=a ,out=AorM);


    // D register   "DR"egister R要大寫
    And(a=isC ,b=d2 ,out=Dload);
    DRegister(in=ALUoutput ,load=Dload ,out=Dout);   //   Dload = isC & d2  =  c丁  


    // ALU  
    ALU(x=Dout ,y=AorM ,zx=c1 ,nx=c2 ,zy=c3 ,ny=c4 ,f=c5 ,no=c6 ,out=ALUoutput ,out=outM ,zr=zr ,ng=ng);  // c戊 = zx、nx、zy、ny、f、no   c己 = zr、ng

    // writeM
    And(a=isC, b=d3, out=writeM);


   // JUMP 
    And(a=ng, b=j1, out=LTJump);  // out小於0
    And(a=zr, b=j2, out=EQJump);  // out等於0
    Or(a=ng, b=zr, out=ngzr);
    Not(in=ngzr, out=gt);         //都不是就是大於了
    And(a=gt, b=j3, out=GTJump);

    Or(a=LTJump, b=EQJump, out=LEJump);
    Or(a=LEJump, b=GTJump, out=jump);

    // PC 
    And(a=isC,b=jump,out=PCload);  // 是C指令 也是jump3本柱之一
    PC(in=ARout, load=PCload, reset=reset, inc=true, out[0..14]=pc);
    // 權重 : reset > load > inc  //所以不是reset不是load的話 inc+1     
    
  

}</code></pre>
